import org.eclipse.jgit.api.Git

group 'com.michaeljperri.flutter_sequencer'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.9.10'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.6.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.eclipse.jgit:org.eclipse.jgit:7.0.0.202409031743-r"
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    namespace "com.michaeljperri.flutter_sequencer"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    compileSdk = 35
    ndkVersion = "27.0.12077973"

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        minSdk 23

        externalNativeBuild {
            cmake {
                arguments "-DCMAKE_CXX_STANDARD=17", "-DCMAKE_CXX_STANDARD_REQUIRED=ON"
                cppFlags '-frelaxed-template-template-args'
            }
        }

        ndk {
            abiFilters "arm64-v8a"
        }
    }
    lintOptions {
        disable 'InvalidPackage'
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

}

void cloneThirdPartyRepo(String name, String uri, String commit) {
    def dirPath = "$projectDir/third_party/$name"
    def dir = new File(dirPath)

    if (!dir.exists()) {
        dir.mkdirs()
        def repo =
                Git.cloneRepository()
                        .setDirectory(dir)
                        .setURI(uri)
                        .setRemote('origin')
                        .setCloneSubmodules(true)
                        .call()

        repo.checkout().setName(commit).call()
        repo.submoduleUpdate()
        repo.submoduleInit()
    }
}

task cloneThirdPartyRepos {
    cloneThirdPartyRepo('oboe', 'https://github.com/google/oboe', 'e40043061cc94df965fda802938b0989f201e86c')
    cloneThirdPartyRepo('TinySoundFont', 'https://github.com/schellingb/TinySoundFont.git', '790a219810cb0fca5defa8cdbd88e2487e5efc7a')
    cloneThirdPartyRepo('sfizz', 'https://github.com/sfztools/sfizz.git', 'f5c6e29f23b8057867c08e88f5f6ac6738baa30b')
}

preBuild.dependsOn cloneThirdPartyRepos
